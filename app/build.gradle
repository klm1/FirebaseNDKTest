apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// set the following three values as appropriate
def symbolCacheDir = new File(rootDir, 'native-library/build/intermediates/crashlyticsSymbolCache')
def alphaToolDir = "/usr/local/lib"
def googleAppId = "x:xxxxxxxxxxxx:android:xxxxxxxxxxxxxxxx"

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.example.firebasendktest"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig debug.signingConfig

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                strippedNativeLibsDir '../native-library/build/intermediates/stripped_native_libs/release/out'
                unstrippedNativeLibsDir '../native-library/build/intermediates/cmake/release/obj'
            }

        }
    }

    buildFeatures {
        viewBinding true
    }
}

task genNativeSymbols(type:Exec) {
	description "Generate the .cSYM file in SYMBOL_CACHE_DIR"

	executable = "java"
	args = [
		"-jar",
		"${alphaToolDir}/crashlytics-buildtools.jar",
        "-generateNativeSymbols",
		"-strippedLibrariesDir",
        "${rootDir}/native-library/build/intermediates/stripped_native_libs/release/out",
		"-unstrippedLibrariesDir",
        "${rootDir}/native-library/build/intermediates/cmake/release/obj",
		"-symbolFileCacheDir=${symbolCacheDir}",
        "-verbose"
	]

    doFirst {
        delete "$symbolCacheDir"
    }
}

task pushNativeSymbols(type:Exec) {
	description "Upload all .cSYM files to Crashlytics servers and associate with the specified app."
    dependsOn "genNativeSymbols"

	executable = "java"
	args = [
		"-jar",
		"${alphaToolDir}/crashlytics-buildtools.jar",
        "-uploadNativeSymbols",
		"-androidApplicationId=com.example.firebasendktest",
		"-googleAppId=${googleAppId}",
		"-symbolFileCacheDir=$symbolCacheDir",
        "-verbose"
	]
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':native-library')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Firebase SDK for Google Analytics.
    implementation 'com.google.firebase:firebase-analytics:17.4.3'

    // Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics-ndk:17.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}